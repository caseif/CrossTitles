// Apply Gradle plugins
plugins {
    id 'java'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'

    id 'org.cadixdev.licenser' version '0.6.1'
}

apply plugin: 'checkstyle'

defaultTasks 'clean', 'licenseFormat', 'build'

// Project information
group = 'net.caseif.crosstitles'
version = '0.1.3-SNAPSHOT'

// Extended project information
ext.projectName = 'crosstitles'
ext.description = 'A Minecraft version-independent title API for Bukkit 1.8+.'
ext.inceptionYear = '2015'
ext.packaging = 'jar'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
    bukkit = 'org.bukkit:bukkit:1.8-R0.1-SNAPSHOT'
}

// Project repositories
repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
}

configurations {
    deployerJars
}

// Project dependencies
dependencies {
    implementation bukkit

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.8'
}

// Read source files using UTF-8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    from 'crosstitles-LICENSE'
}

// License header formatting
license {
    header = project.file('etc/HEADER.txt')
    include '**/*.java'
    ignoreFailures false
}

// check code style
checkstyle {
    configProperties = [
            "name"        : project.name,
            "year"        : project.inceptionYear
    ]
    configFile = file("etc/checkstyle.xml")
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'CrossTitles'
                description = project.ext.description
                url = 'http://github.com/caseif/CrossTitles'

                scm {
                    url = 'https://github.com/caseif/CrossTitles'
                    connection = 'scm:git:git://github.com/caseif/CrossTitles.git'
                    developerConnection = 'scm:git:git@github.com:caseif/CrossTitles.git'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '7.4'
}
